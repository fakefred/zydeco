# This file implements an interpreter for a simple CBPV language.

# As it is a CBPV language, our syntax distinguishes between values
# and computations
data SynVal where
  | Var(String)
  | True()
  | False()
  | ThunkV(SynComp)

data SynComp where
  | If(SynVal, SynComp, SynComp)
  | Force(SynVal)
  | Lam(String, SynComp)
  | App(SynComp, SynVal)
  | Return(SynVal)
  | Bind(SynComp, String, SynComp)

# Environments are implemented as association lists.
data Env where
  | Empty()
  | Cons(String, SemVal, Env)

# The interpreter works with semantic values, which do not include
# variables. We use closures in the interpreter to implement semantic
# thunks.
data SemVal where
  | True'()
  | False'()
  | ThunkV'(Thunk(SemComp))

# The "final answer" of the interpreter is either the final value or
# an error.
data OptionSemVal where
  | Ok(SemVal)
  | Err()

# The semantics of computations are Zydeco computations which satisfy
# this codata type. There are two eliminators: application to a
# variable and a bind.
codata SemComp where
  .app(SemVal) : SemComp
  .bind()      : Ret(OptionSemVal)

# The interpreters for values and computations are implemented
# mutually recursively.
# Currently zydeco doesn't directly support mutual recursion but
# it can be easily simulated:
codata Interpreter where
  .valu(SynVal, Env)  : Ret(OptionSemVal)
  .comp(SynComp, Env) : SemComp

let sem_val_to_str = {
    fn (s : SemVal)  ->
    match s
    | True'() -> ret "true"
    | False'() -> ret "false"
    | ThunkV'(_) -> ret "<thunk>"
};

let rec lookup: String -> Env -> Ret(OptionSemVal) =
  fn (x : String, g : Env) ->
    match g
    | Empty() -> ret Err()
    | Cons(y,v,g) -> (
      do b <- ! str_eq x y;
      if b: ret Ok(v)
      else: ! lookup x g
    );

let rec error: SemComp = (
  comatch
    | .app(_) -> ! error
    | .bind() -> ret Err()
);

let rec interp: Interpreter =
  comatch
    | .valu(v, g) -> (
      match v
      | Var(x) -> ! lookup x g
      | True() -> ret Ok(True'())
      | False() -> ret Ok(False'())
      # Here we use the meta-language closures to capture the environment g 
      | ThunkV(m) -> ret Ok(ThunkV'({ ! interp .comp(m, g) }))
    )
    | .comp(m, g) -> (
      match m
      | If(cond, mt, mf) -> (
        match cond
        | True() -> ! interp .comp(mt, g)
        | False() -> ! interp .comp(mf, g)
      )
      | Force(syn_val) -> (
        do v? <- ! interp .valu(syn_val, g);
        match v?
        | Ok(v) -> (
          match v
          | ThunkV'(t) -> ! t
          | True'() -> ! error
          | False'() -> ! error
        )
        | Err() -> ! error
      )
      | Return(syn_val) -> (
        do v <- ! interp .valu(syn_val, g);
        comatch
          | .bind() -> ret v
          | .app(_) -> ! error
      )
      | Bind(m, x, k) -> (
        do v? <- ! interp .comp(m, g) .bind();
        match v?
        | Ok(v) -> ! interp .comp(k, Cons(x, v, g))
        | Err() -> ! error
      )
      | Lam(x, m) -> (
        comatch
          | .app(v) -> (! interp .comp(m, Cons(x, v, g)))
          | .bind() -> ret Err()
      )
      | App(m, v) -> (
        do v? <- ! interp .valu(v, g);
        match v?
        | Ok(v) -> ! interp .comp(m, g) .app(v)
        | Err() -> ! error
      )
    );

# An example
do x <- ! interp .comp(App(Lam("x", Return(Var("x"))), False()), Empty()) .bind();
match x
| Err() -> ! write_line "ERROR" { ! exit 1 }
| Ok(sv) ->
  (do s <- ! sem_val_to_str sv;
      ! write_line s { ! exit 0 })
